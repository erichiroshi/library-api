plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id "org.sonarqube" version "7.2.2.6593"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Library Service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = [
                'src/main/java',
                "$buildDir/generated/sources/annotationProcessor/java/main"
            ]
        }
    }
 
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	
	integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

ext {
    mapstructVersion = "1.6.3"
    lombokMapstructBindingVersion = "0.2.0"
   	testcontainersVersion = "1.19.7"
}

dependencies {

    // Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'

    // Actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // H2 DataBase
	testImplementation 'com.h2database:h2'
	
	// PostgreSQL Database
	runtimeOnly 'org.postgresql:postgresql'
	
	// Flyway
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.flywaydb:flyway-database-postgresql'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // Caching + Redis
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

 	// MapStruct + Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

	// OpenAPI / Swagger	
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
    	
	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
   	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
   	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	
	// Testcontainers
	testImplementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
	testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    
    // Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'
	
}

jacoco {
    toolVersion = "0.8.14"
}

jacocoTestReport {
    dependsOn test
    
    reports {
       xml.required = true
       html.required = true
    }
    
    afterEvaluate {
        classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        // DTOs
                        '**/dto/**',

                        // Configurações
                        '**/config/**',

                        // MapStruct
                        '**/mapper/**',
                        '**/*MapperImpl.class',

                        // Main / Bootstrap
                        '**/*Application.class',

                        // Exception / Error
                        '**/exception/**',

                        // Enums simples
                        '**/enums/**'
                ])
            })
        )
    }
}

test {
	useJUnitPlatform()
	systemProperty "spring.profiles.active", "test"
	finalizedBy jacocoTestReport
}

jacocoTestCoverageVerification {
    dependsOn test

    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.30
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'
    
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    
    useJUnitPlatform()
    shouldRunAfter test
}

tasks.named('check') {
    dependsOn tasks.integrationTest
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

sonar {
  properties {
    property "sonar.projectKey", "library-api"
    property "sonar.organization", "erichiroshi"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}