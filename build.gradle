plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Spring Library Book Repository'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = [
                'src/main/java',
                "$buildDir/generated/sources/annotationProcessor/java/main"
            ]
        }
    }
 
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	
	integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

ext {
    mapstructVersion = "1.6.3"
    lombokMapstructBindingVersion = "0.2.0"
}

dependencies {

    // Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // H2 DataBase
	runtimeOnly 'com.h2database:h2'
	implementation 'org.springframework.boot:spring-boot-h2console'
	
	// PostgreSQL Database
	runtimeOnly 'org.postgresql:postgresql'
	
	// Flyway
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.flywaydb:flyway-database-postgresql'

 	// Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MapStruct
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // Integração Lombok + MapStruct
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

	// OpenAPI / Swagger	
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
	
	// Spring Security + JWT
	implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    
    // Caching + Redis
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    	
	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	
	// Testcontainers
	testImplementation platform("org.testcontainers:testcontainers-bom:1.19.7")
	testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    
    // Actuator + Prometheus
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
	
}

jacoco {
    toolVersion = "0.8.14"
}

jacocoTestReport {
    dependsOn test
    
    reports {
       xml.required = true
       html.required = true
       csv.required = false
    }
    
     afterEvaluate {
        classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        // DTOs
                        '**/dto/**',

                        // Configurações
                        '**/config/**',

                        // MapStruct
                        '**/mapper/**',
                        '**/*MapperImpl.class',

                        // Main / Bootstrap
                        '**/*Application.class',

                        // Exception / Error
                        '**/exception/**',

                        // Enums simples
                        '**/enums/**'
                ])
            })
        )
    }
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestCoverageVerification {

    dependsOn test

    violationRules {

        rule {
            element = 'BUNDLE'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.39
            }

            excludes = [
                    '**/dto/**',
                    '**/config/**',
                    '**/mapper/**',
                    '**/*MapperImpl.class',
                    '**/*Application.class',
                    '**/exception/**',
                    '**/enums/**'
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

tasks.named('test') {
    useJUnitPlatform()
    systemProperty "spring.profiles.active", "test"
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'
    
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    
    useJUnitPlatform()
    
    shouldRunAfter test
}

tasks.named('check') {
    dependsOn tasks.integrationTest
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

