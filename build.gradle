plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.3'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id "org.sonarqube" version "7.2.2.6593"
    id 'com.google.cloud.tools.jib' version '3.5.3'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Library Service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

jib {
    to {
        image = "library-api:${version}"
    }
}

repositories {
	mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = [
                'src/main/java',
				layout.buildDirectory.dir("generated/sources/annotationProcessor/java/main").get().asFile
            ]
        }
    }
 
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	
	integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

ext {
    mapstructVersion = "1.6.3"
    lombokMapstructBindingVersion = "0.2.0"
    springdocVersion = "3.0.1"
    jjwtVersion = "0.13.0"
   	testcontainersVersion = "2.0.3"
   	awsS3Version = "2.42.2"
}

dependencies {

    // Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'

    // Actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// PostgreSQL Database
	runtimeOnly 'org.postgresql:postgresql'
	
	// Flyway
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.flywaydb:flyway-database-postgresql'

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

    // Caching + Redis
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

 	// MapStruct + Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

	// OpenAPI / Swagger	
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    	
	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
   	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
   	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
   	
   	// Test Security
	testImplementation("org.springframework.security:spring-security-test")
	
	// Testcontainers
	testImplementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
	testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
	testImplementation 'org.testcontainers:testcontainers-jdbc'
    
    // Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'
    
    // AWS SDK (para integração com S3)
	implementation "software.amazon.awssdk:s3:${awsS3Version}"

}

// ─────────────────────────────────────────────
// INTEGRATION TEST TASK
// ─────────────────────────────────────────────

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'
    
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    
    useJUnitPlatform()
    systemProperty "spring.profiles.active", "it"
    shouldRunAfter test

    // Gera o .exec separado para permitir consolidação com unit tests
    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/integrationTest.exec").get().asFile
    }
    finalizedBy jacocoTestReport
}

tasks.named('check') {
    dependsOn tasks.integrationTest
}

// ─────────────────────────────────────────────
// JACOCO
// ─────────────────────────────────────────────

jacoco {
    toolVersion = "0.8.14"
}

// Centralizar exclusões
ext {
    coverageExclusions = [
        '**/dto/**',
        '**/config/**',
        '**/mapper/**',
        '**/*MapperImpl.class',
        '**/*Application.class'
    ]
}

tasks.named("jacocoTestReport") {
    dependsOn(tasks.named("test"))
    dependsOn(tasks.named("integrationTest"))

    reports {
       xml.required = true   // Necessário para SonarCloud e Codecov
       html.required = true  // Útil para visualização local
    }

    // Consolida os execution data de unit + integration tests
    executionData.setFrom(
        fileTree(layout.buildDirectory).include(
            "jacoco/test.exec",
            "jacoco/integrationTest.exec"
        )
    )
    
    classDirectories.setFrom(
        files(
            sourceSets.main.output.asFileTree.matching {
                exclude(coverageExclusions)
            }
        )
    )
    
}

// ─────────────────────────────────────────────
// TESTES
// ─────────────────────────────────────────────

test {
	useJUnitPlatform()
	systemProperty "spring.profiles.active", "test"
	finalizedBy jacocoTestReport
}

tasks.named("jacocoTestCoverageVerification") {
    dependsOn(tasks.named("jacocoTestReport"))

    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }

    // Consolida os execution data — mesma fonte do jacocoTestReport
    executionData.setFrom(
        fileTree(layout.buildDirectory).include(
            "jacoco/test.exec",
            "jacoco/integrationTest.exec"
        )
    )
}

check.dependsOn jacocoTestCoverageVerification

// ─────────────────────────────────────────────
// MISC
// ─────────────────────────────────────────────

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test).configureEach {
    jvmArgs += "--enable-preview"
}

// ─────────────────────────────────────────────
// SONARCLOUD
// ─────────────────────────────────────────────

sonar {
  properties {
    property "sonar.projectKey", "library-api"
    property "sonar.organization", "erichiroshi"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.java.source", "25"
    property "sonar.java.enablePreview", "true"
    property "sonar.exclusions", coverageExclusions.join(',')
    
    property "sonar.coverage.jacoco.xmlReportPaths",
             "build/reports/jacoco/test/jacocoTestReport.xml"
  }
}